PREFIX rr: <http://www.w3.org/ns/r2rml#> 
PREFIX dc: <http://purl.org/dc/elements/1.1/> 
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX rml: <http://semweb.mmlab.be/ns/rml#> 
PREFIX ql: <http://semweb.mmlab.be/ns/ql#> 
PREFIX d2rq: <http://www.wiwiss.fu-berlin.de/suhl/bizer/D2RQ/0.1#> 
PREFIX drugbank_vocabulary: <http://bio2rdf.org/drugbank_vocabulary:> 
PREFIX clinicaltrials_vocabulary: <http://bio2rdf.org/clinicaltrials_vocabulary:> 
PREFIX go_vocabulary: <http://bio2rdf.org/go_vocabulary:> 
PREFIX goa_vocabulary: <http://bio2rdf.org/goa_vocabulary:> 
PREFIX bio2rdf_vocabulary: <http://bio2rdf.org/bio2rdf_vocabulary:> 
PREFIX d2sgeneric: <https://w3id.org/data2services/model/>

INSERT
{
	GRAPH <https://w3id.org/data2services/graph/goa>{
		
		?uri	a goa_vocabulary:Resource;
			rdf:type goa_vocabulary:GO-Annotation;
			dc:identifier ?id;
			rdfs:label ?label;
			dc:title ?Db_obj_name;
			bio2rdf_vocabulary:namespace "goa";
			bio2rdf_vocabulary:identifier ?id;
			bio2rdf_vocabulary:uri ?uri;

			goa_vocabulary:symbol ?Db_symbol;
			goa_vocabulary:x-taxonomy ?Taxon;
			goa_vocabulary:synonym ?Db_obj_synonym;
			goa_vocabulary:symbol ?Db_symbol;
			?aspect_predicate ?Go_id;
			goa_vocabulary:entry-date ?entry_date;
			goa_vocabulary:article ?ref;
			goa_vocabulary:assigned-by ?Assigned_by;	
			
	}
}
WHERE {
    GRAPH <https://w3id.org/data2services/graph/autor2rml/goa> {
		
		?s rdf:type <https://w3id.org/data2services/data/input/goa/goa_human.tsv>.

		?s d2sgeneric:Db ?db .

		?s d2sgeneric:Db_id ?db_id .

		?s d2sgeneric:Db_symbol ?Db_symbol .

		?s d2sgeneric:Taxon ?Taxon .

		?s d2sgeneric:Db_obj_synonym ?Db_obj_synonym .

		?s d2sgeneric:Qualifier ?Qualifier .

		?s d2sgeneric:Aspect ?Aspect_letter .


		?s d2sgeneric:Go_id ?Go .

		BIND( CONCAT("go:",SUBSTR(?Go, 4)) AS ?Go_id).

		?s d2sgeneric:Evidence ?Evidence .

		?s d2sgeneric:Db_obj_name ?Db_obj_name .

		?s d2sgeneric:Assigned_by ?Assigned_by .

		?s d2sgeneric:Date ?Date .
		BIND( CONCAT(SUBSTR(?Date, 1,4),"-",SUBSTR(?Date, 5,2),"-",SUBSTR(?Date, 7,2),"T00:00:00Z") AS ?entry_date).

		?s d2sgeneric:Db_ref ?Db_ref .


		BIND( IF(str(?db) = "UniProtKB" ,CONCAT("uniprot:",?db_id), IF(str(?db) = "PDB", CONCAT("pdb:",STRBEFORE(?db_id,"_"),"/chain_",STRAFTER(?db_id,"_")),CONCAT(?db,":",?db_id))) AS ?id)

		BIND ( concat(?Db_obj_name, " [goa:", ?id, "] ") AS ?label )


		BIND( IF(str(?Aspect_letter) = "P" ,"process", IF(str(?Aspect_letter) = "F" , "function",IF(str(?Aspect_letter) = "C" , "component","Aspect"))) AS ?Aspect)


		BIND( iri(CONCAT("http://bio2rdf.org/goa_vocabulary:", IF(str(?Qualifier) = "NOT" && str(?Aspect) = "process" ,"not-in-process", IF(str(?Qualifier) = "NOT" && str(?Aspect) = "function" , "not-in-function",IF(str(?Qualifier) = "NOT" && str(?Aspect) = "component" , "not-in-component",?Aspect))))) AS ?aspect_predicate)

		BIND( iri(CONCAT("http://bio2rdf.org/",IF(STRBEFORE(?Db_ref,":") = "PMID" ,CONCAT("pubmed:",STRAFTER(?Db_ref,":")), LCASE(?Db_ref)))) AS ?ref)

		BIND ( iri(concat("http://bio2rdf.org/", ?id)) AS ?uri )
		
	}
}		

