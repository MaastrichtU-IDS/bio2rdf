PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX drugbank_vocabulary: <http://bio2rdf.org/drugbank_vocabulary:>
PREFIX bio2rdf_vocabulary: <http://bio2rdf.org/bio2rdf_vocabulary:> 

INSERT 
{ 
    GRAPH <https://w3id.org/data2services/graph/drugbank> {  

	?uri	a drugbank_vocabulary:Resource;
		rdf:type drugbank_vocabulary:Drug-Drug-Interaction;
		dc:identifier ?dcId;
		rdfs:label ?label;
		dc:title ?title;
		bio2rdf_vocabulary:namespace "drugbank_resource";
		bio2rdf_vocabulary:identifier ?itemId;
		bio2rdf_vocabulary:uri ?uri;
		drugbank_vocabulary:interaction-name ?drugInteractionName;
		drugbank_vocabulary:interaction-description ?drugInteractionDesc;
    }
}
WHERE {
  
  GRAPH <https://w3id.org/data2services/graph/xml2rdf> {

	?drugObj a x2rm:drugbank.drug .

        # Get drug primary ID        
	?drugObj x2rm:hasChild [
            a x2rm:drugbank.drug.drugbank-id ;
            x2rm:hasAttribute [ a x2rm:drugbank.drug.drugbank-id.-primary ] ;
            x2rm:hasValue ?drugId
        ].

	?drugObj x2rm:hasChild ?drugInteractions.

	?drugInteractions a x2rm:drugbank.drug.drug-interactions. 

	    OPTIONAL{
	    	?drugInteractions x2rm:hasChild ?drugInteraction.
		?drugInteraction a x2rm:drugbank.drug.drug-interactions.drug-interaction; 

		OPTIONAL{
			?drugInteraction x2rm:hasChild [
			    a x2rm:drugbank.drug.drug-interactions.drug-interaction.drugbank-id; 
			    x2rm:hasValue ?drugInteractionDBid
		    	];
		} OPTIONAL{
			?drugInteraction x2rm:hasChild [
			    a x2rm:drugbank.drug.drug-interactions.drug-interaction.name; 
			    x2rm:hasValue ?drugInteractionName
		    	];
		} OPTIONAL{
			?drugInteraction x2rm:hasChild [
			    a x2rm:drugbank.drug.drug-interactions.drug-interaction.description; 
			    x2rm:hasValue ?drugInteractionDesc;
		    	];
		} # drug interaction features
	    } # drug interaction

	FILTER(STR(?drugId) < STR(?drugInteractionDBid)).

	BIND ( concat(?drugId, "_", ?drugInteractionDBid) AS ?itemId )
	BIND ( concat("drugbank_resource:", ?itemId) AS ?dcId )
	BIND ( concat("DDI between ", ?drugId, " and ",  ?drugInteractionDBid) AS ?title )
	BIND ( concat("DDI between ", ?drugId, " and ",  ?drugInteractionDBid, " [drugbank_resource:", ?itemId, "] ") AS ?label )
	BIND ( iri(concat("http://bio2rdf.org/drugbank_resource:", ?itemId)) AS ?uri )
  } # graph
  
} # where
