PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX drugbank_vocabulary: <http://bio2rdf.org/drugbank_vocabulary:>
PREFIX bio2rdf_vocabulary: <http://bio2rdf.org/bio2rdf_vocabulary:> 

INSERT 
{ 
    GRAPH <https://w3id.org/data2services/graph/drugbank> {  

	?uri	a drugbank_vocabulary:Resource;
		rdf:type drugbank_vocabulary:Attachment;
		dc:identifier ?dcId;
		rdfs:label ?label;
		dc:title ?title;
		bio2rdf_vocabulary:namespace "drugbank";
		bio2rdf_vocabulary:identifier ?attachmentRefIdValue;
		bio2rdf_vocabulary:uri ?uri;

		drugbank_vocabulary:title ?attachmentTitleValue;
		drugbank_vocabulary:url ?attachmentUrlUri;
    }
}
WHERE {
  
  GRAPH <https://w3id.org/data2services/graph/xml2rdf> {

	?attachment a ?attachmentType.

	OPTIONAL{
		?attachment x2rm:hasChild ?attachmentRefId.
		?attachmentRefId a ?attachmentRefIdType.
		?attachmentRefId x2rm:hasValue ?attachmentRefIdValue.
	} OPTIONAL{
		?attachment x2rm:hasChild ?attachmentTitle.
		?attachmentTitle a ?attachmentTitleType.
		?attachmentTitle x2rm:hasValue ?attachmentTitleValue.
	} OPTIONAL{
		?attachment x2rm:hasChild ?attachmentUrl.
		?attachmentUrl a ?attachmentUrlType.
		?attachmentUrl x2rm:hasValue ?attachmentUrlValue.
	} # Target Ref Articles features
  
	VALUES( ?attachmentType ?attachmentRefIdType ?attachmentTitleType ?attachmentUrlType) {

		(x2rm:drugbank.drug.general-references.attachments.attachment x2rm:drugbank.drug.general-references.attachments.attachment.ref-id x2rm:drugbank.drug.general-references.attachments.attachment.title x2rm:drugbank.drug.general-references.attachments.attachment.url)

		(x2rm:drugbank.drug.targets.target.references.attachments.attachment x2rm:drugbank.drug.targets.target.references.attachments.attachment.ref-id x2rm:drugbank.drug.targets.target.references.attachments.attachment.title x2rm:drugbank.drug.targets.target.references.attachments.attachment.url)

		(x2rm:drugbank.drug.enzymes.enzyme.references.attachments.attachment x2rm:drugbank.drug.enzymes.enzyme.references.attachments.attachment.ref-id x2rm:drugbank.drug.enzymes.enzyme.references.attachments.attachment.title x2rm:drugbank.drug.enzymes.enzyme.references.attachments.attachment.url)

		(x2rm:drugbank.drug.carriers.carrier.references.attachments.attachment x2rm:drugbank.drug.carriers.carrier.references.attachments.attachment.ref-id x2rm:drugbank.drug.carriers.carrier.references.attachments.attachment.title x2rm:drugbank.drug.carriers.carrier.references.attachments.attachment.url)

		(x2rm:drugbank.drug.transporters.transporter.references.attachments.attachment x2rm:drugbank.drug.transporters.transporter.references.attachments.attachment.ref-id x2rm:drugbank.drug.transporters.transporter.references.attachments.attachment.title x2rm:drugbank.drug.transporters.transporter.references.attachments.attachment.url)

	} # VALUES

	BIND ( iri(concat("http:", ?attachmentUrlValue)) AS ?attachmentUrlUri )

	BIND ( concat("drugbank:", ?attachmentRefIdValue) AS ?dcId )
	BIND ( concat("Reference Attachment ", ?attachmentRefIdValue) AS ?title)
	BIND ( concat(?title, " [drugbank:", ?attachmentRefIdValue, "] ") AS ?label )
	BIND ( iri(concat("http://bio2rdf.org/drugbank:", ?attachmentRefIdValue)) AS ?uri )	
  } # graph
  
} # where
