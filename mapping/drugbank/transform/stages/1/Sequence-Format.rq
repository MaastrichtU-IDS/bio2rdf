PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX drugbank_vocabulary: <http://bio2rdf.org/drugbank_vocabulary:>
PREFIX bio2rdf_vocabulary: <http://bio2rdf.org/bio2rdf_vocabulary:> 

INSERT 
{ 
    GRAPH <https://w3id.org/data2services/graph/drugbank> {  

	?uri	a drugbank_vocabulary:Resource;
		rdf:type drugbank_vocabulary:Sequence-Format;
		dc:identifier ?dcId;
		rdfs:label ?label;
		dc:title ?sequenceFormat;
		bio2rdf_vocabulary:namespace "drugbank_resource";
		bio2rdf_vocabulary:identifier ?itemId;
		bio2rdf_vocabulary:uri ?uri;
    }
}
WHERE {
  
  GRAPH <https://w3id.org/data2services/graph/xml2rdf> {

	?sequence a ?sequenceType.
	
	?sequence  x2rm:hasAttribute ?sequenceFormatAttr.
	?sequenceFormatAttr x2rm:hasValue ?sequenceFormat.

	VALUES( ?sequenceType) {

		(x2rm:drugbank.drug.sequences.sequence) 
		(x2rm:drugbank.drug.targets.target.polypeptide.gene-sequence) 
		(x2rm:drugbank.drug.enzymes.enzyme.polypeptide.gene-sequence) 
		(x2rm:drugbank.drug.carriers.carrier.polypeptide.gene-sequence) 
		(x2rm:drugbank.drug.transporters.transporter.polypeptide.gene-sequence) 
		(x2rm:drugbank.drug.targets.target.polypeptide.amino-acid-sequence) 
		(x2rm:drugbank.drug.enzymes.enzyme.polypeptide.amino-acid-sequence) 
		(x2rm:drugbank.drug.carriers.carrier.polypeptide.amino-acid-sequence) 
		(x2rm:drugbank.drug.transporters.transporter.polypeptide.amino-acid-sequence)

	} # VALUES

	BIND ( REPLACE(UCASE(STR(?sequenceFormat)), " ", "-") AS ?itemId)
	BIND ( concat("drugbank_resource:", ?itemId) AS ?dcId )
	BIND ( concat(?sequenceFormat, " [drugbank_resource:", ?itemId, "] ") AS ?label )
	BIND ( iri(concat("http://bio2rdf.org/drugbank_resource:", ?itemId)) AS ?uri )
  } # graph
  
} # where
