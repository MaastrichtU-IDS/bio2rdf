PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX drugbank_vocabulary: <http://bio2rdf.org/drugbank_vocabulary:>
PREFIX bio2rdf_vocabulary: <http://bio2rdf.org/bio2rdf_vocabulary:> 

INSERT 
{ 
    GRAPH <https://w3id.org/data2services/graph/drugbank> {  

	?uri	a drugbank_vocabulary:Resource;
		rdf:type drugbank_vocabulary:Article;
		dc:identifier ?dcId;
		rdfs:label ?label;
		dc:title ?title;
		bio2rdf_vocabulary:namespace "drugbank";
		bio2rdf_vocabulary:identifier ?articleRefIdValue;
		bio2rdf_vocabulary:uri ?uri;

		drugbank_vocabulary:x-pubmed ?articlePubmedIdUri;
		drugbank_vocabulary:citation ?articleCitationValue;
    }
}
WHERE {
  
  GRAPH <https://w3id.org/data2services/graph/xml2rdf> {

	?article a ?articleType.

	OPTIONAL{
		?article x2rm:hasChild ?articleRefId.
		?articleRefId a ?articleRefIdType.
		?articleRefId x2rm:hasValue ?articleRefIdValue.
	} OPTIONAL{
		?article x2rm:hasChild ?articlePubmedId.
		?articlePubmedId a ?articlePubmedIdType.
		?articlePubmedId x2rm:hasValue ?articlePubmedIdValue.
	} OPTIONAL{
		?article x2rm:hasChild ?articleCitation.
		?articleCitation a ?articleCitationType.
		?articleCitation x2rm:hasValue ?articleCitationValue.
	} # Target Ref Articles features
  
	VALUES( ?articleType ?articleRefIdType ?articlePubmedIdType ?articleCitationType) {

		(x2rm:drugbank.drug.general-references.articles.article x2rm:drugbank.drug.general-references.articles.article.ref-id x2rm:drugbank.drug.general-references.articles.article.pubmed-id x2rm:drugbank.drug.general-references.articles.article.citation)

		(x2rm:drugbank.drug.targets.target.references.articles.article x2rm:drugbank.drug.targets.target.references.articles.article.ref-id x2rm:drugbank.drug.targets.target.references.articles.article.pubmed-id x2rm:drugbank.drug.targets.target.references.articles.article.citation)

		(x2rm:drugbank.drug.enzymes.enzyme.references.articles.article x2rm:drugbank.drug.enzymes.enzyme.references.articles.article.ref-id x2rm:drugbank.drug.enzymes.enzyme.references.articles.article.pubmed-id x2rm:drugbank.drug.enzymes.enzyme.references.articles.article.citation)

		(x2rm:drugbank.drug.carriers.carrier.references.articles.article x2rm:drugbank.drug.carriers.carrier.references.articles.article.ref-id x2rm:drugbank.drug.carriers.carrier.references.articles.article.pubmed-id x2rm:drugbank.drug.carriers.carrier.references.articles.article.citation)

		(x2rm:drugbank.drug.transporters.transporter.references.articles.article x2rm:drugbank.drug.transporters.transporter.references.articles.article.ref-id x2rm:drugbank.drug.transporters.transporter.references.articles.article.pubmed-id x2rm:drugbank.drug.transporters.transporter.references.articles.article.citation)

	} # VALUES


	BIND ( iri(concat("http://bio2rdf.org/pubmed:", ?articlePubmedIdValue)) AS ?articlePubmedIdUri )

	BIND ( concat("drugbank:", ?articleRefIdValue) AS ?dcId )
	BIND ( concat("Reference Article ", ?articleRefIdValue) AS ?title)
	BIND ( concat(?title, " [drugbank:", ?articleRefIdValue, "] ") AS ?label )
	BIND ( iri(concat("http://bio2rdf.org/drugbank:", ?articleRefIdValue)) AS ?uri )	
  } # graph
  
} # where
