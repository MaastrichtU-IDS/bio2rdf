PREFIX d2s: <https://w3id.org/data2services/model/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX w3idvocab: <https://w3id.org/data2services/vocab/>
PREFIX ctd_vocabulary: <http://bio2rdf.org/ctd_vocabulary:>
PREFIX bio2rdf_vocabulary: <http://bio2rdf.org/bio2rdf_vocabulary:> 

INSERT {
  GRAPH <https://w3id.org/data2services/graph/ctd> {  
    # Attribute the retrieved data to your model properties

	?uri	a ctd_vocabulary:Resource;
		rdf:type ctd_vocabulary:Chemical-Gene-Association;
		dc:identifier ?identifier;
		rdfs:label ?label;
		dc:title ?title;
		bio2rdf_vocabulary:namespace "ctd";
		bio2rdf_vocabulary:identifier ?id;
		bio2rdf_vocabulary:uri ?uri;

		ctd_vocabulary:gene ?gene_uri ;
		ctd_vocabulary:chemical ?chemical_uri ;
		rdfs:comment ?Interaction ;
		ctd_vocabulary:organism ?organism_uri ;
		ctd_vocabulary:article ?pubmed_uri ;
		ctd_vocabulary:action ?Interactionactions ;

 }
} WHERE {
    GRAPH <https://w3id.org/data2services/graph/autor2rml/ctd> {

	?s rdf:type <https://w3id.org/data2services/data/input/ctd/CTD_chem_gene_ixns.tsv> .

	?s d2s:Chemicalname ?Chemicalname .
	?s d2s:Chemicalid ?Chemicalid . 

	?s d2s:Genesymbol ?Genesymbol .
	?s d2s:Geneid ?Geneid . 

	BIND ( concat(?Chemicalid,?Geneid) AS ?id )

	BIND ( concat("ctd_resource:",?id) AS ?identifier )

	BIND ( concat("association between ", ?Genesymbol, " and ",?Chemicalname) AS ?title )

	BIND ( concat("association between ", ?Genesymbol, " (ncbigene:",?Geneid,") and ",?Chemicalname," (mesh:",?Chemicalid,")") AS ?label )


	BIND(iri(concat("http://bio2rdf.org/ncbigene:", ?Geneid)) AS ?gene_uri)
	BIND(iri(concat("http://bio2rdf.org/mesh:", ?Chemicalid)) AS ?chemical_uri)

	OPTIONAL { 
		?s d2s:Organismid ?Organismid . 
		BIND(iri(concat("http://bio2rdf.org/taxon:", ?Organismid)) AS ?organism_uri)
	}

	OPTIONAL { ?s d2s:Interaction ?Interaction . }

	OPTIONAL { ?s d2s:Interactionactions ?Interactionactions . }

	OPTIONAL { 
		?s d2s:Pubmedids ?Pubmedids .
		BIND(iri(concat("http://bio2rdf.org/pubmed:", ?Pubmedids)) AS ?pubmed_uri) 
	}

	
	BIND ( iri(concat("http://bio2rdf.org/ctd_resource:", ?Chemicalid, ?Geneid)) AS ?uri )
  }
}
